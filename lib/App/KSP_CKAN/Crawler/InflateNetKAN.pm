package App::KSP_CKAN::Crawler::InflateNetKAN;

use v5.010;
use strict;
use warnings;
use autodie;
use Method::Signatures 20140224;
use File::chdir;
use Scalar::Util 'reftype';
use Carp qw(croak);
use Moo;
use namespace::clean;

extends 'App::KSP_CKAN::NetKAN';

# ABSTRACT: Used as part of the Crawler to re-inflate CKANs

# VERSION: Generated by DZP::OurPkg:Version

=head1 SYNOPSIS

  use App::KSP_CKAN::Crawler::InflateNetKAN;

  my $inflater = App::KSP_CKAN::Crawler::InflateNetKAN->new( config => $confg );
  $inflater->inflate( "AwesomeMod.netkan" );

=head1 DESCRIPTION

Webhook wrapper for NetKAN inflation on demand.

=cut

my $Ref = sub {
  croak("config isn't a 'App::KSP_CKAN::Tools::Config' object!") unless $_[0]->DOES("App::KSP_CKAN::Tools::Config");
};

has 'config' => ( is => 'ro', required => 1, isa => $Ref );

method inflate($files) {
  # Lets take an array as well! 
  my @files = reftype \$files ne "SCALAR" ? @{$files} : $files;

  # Prepare Enironment
  $self->_mirror_files;
  $self->_CKAN_meta->_clean; # TODO: expose this method properly
  $self->_CKAN_meta->pull;

  foreach my $file (@files) {
    if (! -e $file) {
      $self->warn("The file '".$file."' doesn't appear to exist");
      next;
    }

    my $config = $self->config;
    my $netkan = App::KSP_CKAN::Tools::NetKAN->new(
      config      => $config,
      file        => $file,
      ckan_meta   => $self->_CKAN_meta,
      status      => $self->_status,
      rescan      => 1,
    );
    $netkan->inflate;
  }

  $self->_push;

  # TODO: We're never going to want a status on the main project but
  #       we can probably replace it with something of in here.
  return 1;
}

1;
